// This alerts when memory usage on the host is past a certaint threshold.
var db = 'telegraf'
var rp = 'two_weeks'

var measurement = 'sysstat_mem_util'
var groupBy = []
var whereFilter = lambda: TRUE

var name = 'Memory'

var idVar = name + ':{{.Group}}'
var message = '[{{.Level}}] High memory usage at {{ index .Fields "value" | printf "%0.2f"}}%'
var idTag = 'alertID'
var levelTag = 'level'
var messageField = 'message'
var durationField = 'duration'

var outputDB = 'chronograf'
var outputRP = 'autogen'
var outputMeasurement = 'alerts'
var triggerType = 'threshold'
var warn = 80
var crit = 90

var data = stream
    |from()
        .database(db)
        .retentionPolicy(rp)
        .measurement(measurement)
        .groupBy(groupBy)
        .where(whereFilter)
    |eval(lambda: ("kbmemused" - "kbbuffers" - "kbcached") / ("kbmemused" + "kbmemfree") * 100.0)
        .as('value')

var trigger = data
    |alert()
        .warn(lambda: "value" > warn)
        .crit(lambda: "value" > crit)
        .stateChangesOnly()
        .message(message)
        .id(idVar)
        .idTag(idTag)
        .levelTag(levelTag)
        .messageField(messageField)
        .durationField(durationField)
        .log('/tmp/alerts.log')

trigger
    |influxDBOut()
        .create()
        .database(outputDB)
        .retentionPolicy(outputRP)
        .measurement(outputMeasurement)
        .tag('alertName', name)
        .tag('triggerType', triggerType)

trigger
    |httpOut('output')
