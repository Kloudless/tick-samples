// This script is meant to alert when the host is experiencing CPU steal
var db = 'telegraf'
var rp = 'two_weeks'
var measurement = 'sysstat_cpu'

var groupBy = []
var whereFilter = lambda: TRUE

var name = 'CPU Steal'

var idVar = name + ':{{.Group}}'
var message = '[{{.Level}}] High CPU steal% at {{ index .Fields "value" | printf "%0.2f"}}%'
var idTag = 'alertID'
var levelTag = 'level'
var messageField = 'message'
var durationField = 'duration'

var outputDB = 'chronograf'
var outputRP = 'autogen'
var outputMeasurement = 'alerts'

var triggerType = 'threshold'

var warn = 40
var crit = 60

var data = stream
    |from()
        .database(db)
        .retentionPolicy(rp)
        .measurement(measurement)
        .groupBy(groupBy)
        .where(whereFilter)
    |eval(lambda: "pct_pct_steal")
        .as('value')

var trigger = data
    |alert()
        .warn(lambda: "value" > warn)
        .crit(lambda: "value" > crit)
        .stateChangesOnly()
        .message(message)
        .id(idVar)
        .idTag(idTag)
        .levelTag(levelTag)
        .messageField(messageField)
        .durationField(durationField)
        .log('/tmp/alerts.log')

trigger
    |influxDBOut()
        .create()
        .database(outputDB)
        .retentionPolicy(outputRP)
        .measurement(outputMeasurement)
        .tag('alertName', name)
        .tag('triggerType', triggerType)

trigger
    |httpOut('output')
